Option Explicit
'http://hatenachips.blog34.fc2.com/blog-entry-416.html

Private Declare Function URLDownloadToFile Lib "urlmon" _
                                        Alias "URLDownloadToFileA" ( _
                                        ByVal pCaller As Long, _
                                        ByVal szURL As String, _
                                        ByVal szFileName As String, _
                                        ByVal dwReserved As Long, _
                                        ByVal lpfnCB As Long) As Long

Enum GMMapType 'maptype 地図のスタイル
    roadmap = 1     '通常の地図
    satellite = 2   '航空写真
    terrain = 3     '地形地図
    hybrid = 4      '航空写真+通常の地図
End Enum

'Googleマップ画像ファイルをダウンロードする
Function GetMapImgFile(ByVal ImgFile As String, _
                    ByVal Address As String, _
                    Optional ByVal MapType As GMMapType = roadmap, _
                    Optional ByVal Zoom As Long = 14, _
                    Optional ByVal Width As Integer = 400, _
                    Optional ByVal Height As Integer = 300) As Boolean
    Dim MapURL As String

    MapURL = GetMapImgUrl(Address, MapType, Zoom, Width, Height)
    GetMapImgFile = URLDownload(MapURL, ImgFile)

End Function

'Googleマップ画像のURLを取得する
Function GetMapImgUrl(ByVal Address As String, _
                Optional ByVal MapType As GMMapType = roadmap, _
                Optional ByVal Zoom As Long = 14, _
                Optional ByVal Width As Integer = 400, _
                Optional ByVal Height As Integer = 300) As String

    GetMapImgUrl = "http://maps.google.co.jp/maps/api/staticmap?" & _
                "markers=" & urlEncode(Address) & _
                "&maptype=" & Choose(MapType, "roadmap", "satellite", "terrain", "hybrid") & _
                "&size=" & Width & "x" & Height & _
                "&zoom=" & Zoom & _
                "&size=" & Width & "x" & Height & "&sensor=false"
End Function

'Googleマップ画像ファイルをダウンロードする
Function GetRouteMapImgFile(ByVal ImgFile As String, _
                    ByVal Address As String, _
                    Optional ByVal MapType As GMMapType = roadmap, _
                    Optional ByVal Zoom As Long = 14, _
                    Optional ByVal Width As Integer = 400, _
                    Optional ByVal Height As Integer = 300) As Boolean
    Dim MapURL As String

    MapURL = GetRouteMapImgUrl(Address, MapType, Zoom, Width, Height)
    GetRouteMapImgFile = URLDownload(MapURL, ImgFile)

End Function


'Google経路マップ画像のURLを取得する
Function GetRouteMapImgUrl(ByVal Address As String, _
                Optional ByVal MapType As GMMapType = roadmap, _
                Optional ByVal Zoom As Long = 14, _
                Optional ByVal Width As Integer = 400, _
                Optional ByVal Height As Integer = 300) As String

    GetRouteMapImgUrl = "http://maps.google.co.jp/maps/dir?" & _
                "path=" & urlEncode("rgba:0x0000ff77,weight:5|" & Address) & _
                "&maptype=" & Choose(MapType, "roadmap", "satellite", "terrain", "hybrid") & _
                "&size=" & Width & "x" & Height & _
                "&zoom=" & Zoom & _
                "&size=" & Width & "x" & Height & "&sensor=false&format=jpg-baseline&/data=!4m2!4m1!3e2"
'&path=rgba:0x0000ff77,weight:5|35.68818,139.77322|35.68400,139.77450|35.67891,139.77176|35.67640,139.77811
End Function




'URLを指定してファイルをダウンロードする
Function URLDownload(URL, FileName As String) As Boolean
    If IsNull(URL) Or URL = "" Then Exit Function
    URLDownload = URLDownloadToFile(0, URL, FileName, 0, 0) = 0
End Function

'URLエンコード
Function urlEncode(str As String) As String
    With CreateObject("ScriptControl")
        .Language = "Jscript"
        urlEncode = .CodeObject.encodeURIComponent(str)
    End With
End Function

'使用例
Public Sub TestGetMapImgFile()
    '複数のマーカーを指定する
    If GetMapImgFile("p:\map.jpg", "東京駅|神田駅") Then
        MsgBox "ダウンロードできました"
    Else
        MsgBox "エラーが発生しました"
    End If

    'パラメータを指定
    If GetMapImgFile("p:\tokyo.jpg", "東京駅", hybrid, 18, 1000, 800) Then
        MsgBox "ダウンロードできました"
    Else
        MsgBox "エラーが発生しました"
    End If

    'Routeを指定
    If GetRouteMapImgFile("p:\route.jpg", "東京駅|秋葉原駅") Then
        MsgBox "ダウンロードできました"
    Else
        MsgBox "エラーが発生しました"
    End If

End Sub

